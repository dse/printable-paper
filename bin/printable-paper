#!/usr/bin/env perl
use warnings;
use strict;
use v5.10.0;

# on Cygwin:
#     apt-cyg install libpaper1 libpaper-bin
# on other systems:
#     it's part of something called libpaper.
#     git@github.com:naota/libpaper.git
# to list all paper sizes and their widths and heights:
#     paperconf -a -n -s
# to get the default paper size:
#     paperconf

our $opt_papersize;
our $opt_width;
our $opt_height;
our $opt_unit_type;
our $opt_color_type;
our @opt_modifiers;
our $opt_list_ruling_names;
our $opt_list_rulings;

our $opt_id;
our $opt_filename;

our $opt_2page_2up;
our $opt_2page_4up;
our $opt_2page;
our $opt_2up;
our $opt_4up;
our $opt_pdf;
our $opt_ps;

our $opt_dry_run;
our $opt_verbose = 0;

our $opt_line_width;
our $opt_major_line_width;
our $opt_feint_line_width;
our $opt_dot_width;
our $opt_margin_line_width;
our $opt_dpi;

our $opt_line_color;
our $opt_major_line_color;
our $opt_feint_line_color;
our $opt_margin_line_color;
our $opt_color;

use lib "$ENV{HOME}/git/dse.d/utilities/lib";
use My::Text::Table;

use Getopt::Long;
use Data::Dumper;

use FindBin;
use lib "${FindBin::Bin}/../lib";

Getopt::Long::Configure(qw(gnu_compat no_getopt_compat no_ignore_case));
Getopt::Long::GetOptions(
    "M|p|media|paper|paper-size=s"    => \$opt_papersize,
    "w|width=s"                       => \$opt_width,
    "h|height=s"                      => \$opt_height,
    'letter'                          => sub { $opt_papersize = 'letter'; },
    'a4'                              => sub { $opt_papersize = 'a4'; },
    'halfletter|half-letter'          => sub { $opt_papersize = 'halfletter'; },
    'a5'                              => sub { $opt_papersize = 'a5'; },
    'quarterletter|quarter-letter'    => sub { $opt_papersize = 'quarterletter'; },
    'a6'                              => sub { $opt_papersize = 'a6'; },
    "g|gray|grey|grayscale|greyscale" => sub { $opt_color_type = "grayscale"; },
    "b|black"                         => sub { $opt_color_type = "black";     },
    "c|color|colour"                  => sub { $opt_color_type = "color";     },
    "m|metric"                        => sub { $opt_unit_type = "metric";     },
    "i|imperial"                      => sub { $opt_unit_type = "imperial";   },
    "D|define|modifier|modifiers=s"   => sub { push(@opt_modifiers, split(qr{\s+|\s*,\s*}, $_[1])); },
    'id=s'                            => \$opt_id,
    'o|output|filename=s'             => \$opt_filename,
    'list-ruling-names'               => \$opt_list_ruling_names,
    'list-rulings'                    => \$opt_list_rulings,
    '2p2u|2page2up|2page-2up|two-page-two-up|generate-two-page-two-up|generate-2page-2up'   => \$opt_2page_2up,
    '2p4u|2page4up|2page-4up|two-page-four-up|generate-two-page-four-up|generate-2page-4up' => \$opt_2page_4up,
    '2p|2page|two-page|generate-two-page|generate-2page'                                    => \$opt_2page,
    '2u|2up|two-up|genreate-two-up|generate-2up'                                            => \$opt_2up,
    '4u|4up|four-up|genreate-four-up|generate-4up'                                          => \$opt_4up,
    'ps|postscript|generate-ps|generate-postscript'                                         => \$opt_ps,
    'pdf|generate-pdf'                                                                      => \$opt_pdf,
    'everything' => sub {
        $opt_2page_2up = 1;
        $opt_2page_4up = 1;
        $opt_2page     = 1;
        $opt_2up       = 1;
        $opt_4up       = 1;
        $opt_ps        = 1;
        $opt_pdf       = 1;
    },
    'n|dry-run'              => \$opt_dry_run,
    'v|verbose+'             => \$opt_verbose,
    'dpi=f'                  => \$opt_dpi,
    'lw|line-width=f'        => \$opt_line_width,
    'mjw|major-line-width=f' => \$opt_major_line_width,
    'fw|feint-line-width=f'  => \$opt_feint_line_width,
    'dw|dot-width=f'         => \$opt_dot_width,
    'mw|margin-line-width=f' => \$opt_margin_line_width,
    'lc|line-color=s'        => \$opt_line_color,
    'mjc|major-line-color=s' => \$opt_major_line_color,
    'fc|feint-line-color=s'  => \$opt_feint_line_color,
    'mc|margin-line-color=s' => \$opt_margin_line_color,
    'C|color=s'              => \$opt_color,
    "help|?" => sub {
        usage();
        exit(0);
    }
) or die("Type '$0 --help' for help.\n");

sub usage { print <<"EOF"; }
usage:
    $0 [<option> ...] <ruling>
to specify the paper size of ONE page:
    -M, -p, --media, --paper-size=<size>        8.5x11in, letter, etc.
        -w, --width=<width>                     0.5in, 36, 36pt, etc.
        -h, --height=<height>
options:
        -g, --gray, --grayscale
        -b, --black
        -c, --color, --colour
        -m, --metric
        -i, --imperial
        -D, --define, --modifier=<modifier>
        -o, --output, --filename=<filename>     output SVG to a file, not stdout
            --id=<id>
list of <ruling>s:
    --list-ruling-names
    --list-rulings              show some details
if --filename is specified:
    --pdf                       also generate PDF file(s)
    --ps                        also generate PostScript file(s)
    --2u,   --two-up            also generate 2-up        file(s) w/ --pdf/--ps
    --4u,   --four-up           also generate 4-up        file(s) w/ --pdf/--ps
    --2p,   --two-page          "    "        2-page      file(s) "
    --2p2u, --two-page-two-up   "    "        2-page 2-up file(s) "
    --2p4u, --two-page-four-up  "    "        2-page 4-up file(s) "
ruling line widths:
           --dpi=<dpi>          printer dots per inch (default 600)
    --lw,  --line-width=<dots>
    --mjw, --major-line-width=<dots>
    --fw,  --feint-line-width=<dots>
    --dw,  --dot-width=<dots>
    --mw,  --margin-line-width=<dots>
ruling line colors:             '#rrggbb[aa]', 'rgb()', 'rgba()' style colors
    --lc,  --line-color=<color>
    --mjc, --major-line-color=<color>
    --fc,  --feint-line-color=<color>
    --mc,  --margin-line-color=<color>
      -C,  --color=<color>      specify color for all lines and dots
miscellaneous:
    -n, --dry-run
    -v, --verbose
    -?, --help
EOF

use My::Printable::Paper::Document;
use My::Printable::Paper::Ruling;
use My::Printable::Paper::Unit qw(:const);

if ($opt_pdf && !defined $opt_filename) {
    die("--pdf has no effect without --filename\n");
}
if ($opt_ps && !defined $opt_filename) {
    die("--ps has no effect without --filename\n");
}
if ($opt_2page_2up && !defined $opt_filename) {
    die("--two-page-two-up has no effect without --filename\n");
}
if ($opt_2page_4up && !defined $opt_filename) {
    die("--two-page-four-up has no effect without --filename\n");
}
if ($opt_2page && !defined $opt_filename) {
    die("--two-page has no effect without --filename\n");
}
if ($opt_2up && !defined $opt_filename) {
    die("--two-up has no effect without --filename\n");
}
if ($opt_4up && !defined $opt_filename) {
    die("--four-up has no effect without --filename\n");
}

if (!$opt_pdf && !$opt_ps) {
    if ($opt_2page_2up) {
        die("--two-page-two-up has no effect without --pdf and/or --ps\n");
    }
    if ($opt_2page_4up) {
        die("--two-page-four-up has no effect without --pdf and/or --ps\n");
    }
    if ($opt_2page) {
        die("--two-page has no effect without --pdf and/or --ps\n");
    }
    if ($opt_2up) {
        die("--two-up has no effect without --pdf and/or --ps\n");
    }
    if ($opt_4up) {
        die("--four-up has no effect without --pdf and/or --ps\n");
    }
}

if ($opt_list_ruling_names || $opt_list_rulings) {
    use My::Printable::Paper::Util::Installed;
    my $inst = My::Printable::Paper::Util::Installed->new();
    if ($opt_list_ruling_names) {
        say foreach @{$inst->rulingNameList};
    }
    if ($opt_list_rulings) {
        my $tt = My::Text::Table->new();
        $tt->header(
            'Ruling',
            'Color Type',
            'Lines',
            'Major Lines',
            'Feint Lines',
            'Dots',
            'Margin Lines',
        );
        foreach my $ruling_name (@{$inst->rulingNameList}) {
            my $count = 0;
            foreach my $colorType ('color', 'grayscale', 'black') {
                $count += 1;
                my $ruling_object = create_ruling_object($ruling_name);
                $ruling_object->colorType($colorType);
                my $lineWidth          = eval { $ruling_object->lineWidth       / PD * PT; } // 0;
                my $majorLineWidth     = eval { $ruling_object->majorLineWidth  / PD * PT; } // 0;
                my $feintLineWidth     = eval { $ruling_object->feintLineWidth  / PD * PT; } // 0;
                my $dotWidth           = eval { $ruling_object->dotWidth        / PD * PT; } // 0;
                my $marginLineWidth    = eval { $ruling_object->marginLineWidth / PD * PT; } // 0;
                $tt->addRow(
                    $count == 1 ? $ruling_name : '',
                    $colorType,
                    sprintf('%.3g', $lineWidth),
                    sprintf('%.3g', $majorLineWidth),
                    sprintf('%.3g', $feintLineWidth),
                    sprintf('%.3g', $dotWidth),
                    sprintf('%.3g', $marginLineWidth),
                );
            }
        }
        print $tt->as_string;
    }
    exit(0);
}

if (scalar @ARGV != 1) {
    die("must specify a ruling name\n");
}

foreach my $ruling_name (@ARGV) {
    my $ruling_object = create_ruling_object($ruling_name);
    $ruling_object->generate();
    $ruling_object->print();
}

sub create_ruling_object {
    my ($ruling_name, @args) = @_;
    my $ruling_class_name = My::Printable::Paper::Ruling->getRulingClassName($ruling_name);
    eval "use $ruling_class_name";
    if ($@) {
        die $@;
    }
    my $ruling_object = $ruling_class_name->new();
    $ruling_object->id($opt_id)                                         if defined $opt_id;
    $ruling_object->filename($opt_filename)                             if defined $opt_filename;
    $ruling_object->paperSizeName($opt_papersize)                       if defined $opt_papersize;
    $ruling_object->width($opt_width)                                   if defined $opt_width;
    $ruling_object->height($opt_height)                                 if defined $opt_height;
    $ruling_object->unitType($opt_unit_type)                            if defined $opt_unit_type;
    $ruling_object->colorType($opt_color_type)                          if defined $opt_color_type;
    $ruling_object->generate2Up($opt_2up)                               if defined $opt_2up;
    $ruling_object->generate4Up($opt_4up)                               if defined $opt_4up;
    $ruling_object->generate2Page($opt_2page)                           if defined $opt_2page;
    $ruling_object->generate2Page2Up($opt_2page_2up)                    if defined $opt_2page_2up;
    $ruling_object->generate2Page4Up($opt_2page_4up)                    if defined $opt_2page_4up;
    $ruling_object->generatePS($opt_ps)                                 if defined $opt_ps;
    $ruling_object->generatePDF($opt_pdf)                               if defined $opt_pdf;
    $ruling_object->dryRun($opt_dry_run)                                if defined $opt_dry_run;
    $ruling_object->verbose($opt_verbose)                               if defined $opt_verbose;
    $ruling_object->dpi($opt_dpi)                                       if defined $opt_dpi;
    $ruling_object->lineWidth($opt_line_width)                          if defined $opt_line_width;
    $ruling_object->majorLineWidth($opt_major_line_width)               if defined $opt_major_line_width;
    $ruling_object->feintLineWidth($opt_feint_line_width)               if defined $opt_feint_line_width;
    $ruling_object->dotWidth($opt_dot_width)                            if defined $opt_dot_width;
    $ruling_object->marginLineWidth($opt_margin_line_width)             if defined $opt_margin_line_width;
    $ruling_object->lineColor($opt_line_color // $opt_color)            if defined $opt_line_color || defined $opt_color;
    $ruling_object->majorLineColor($opt_major_line_color // $opt_color) if defined $opt_major_line_color || defined $opt_color;
    $ruling_object->feintLineColor($opt_feint_line_color // $opt_color) if defined $opt_feint_line_color || defined $opt_color;
    $ruling_object->marginLineColor($opt_margin_line_color)             if defined $opt_margin_line_color;
    $ruling_object->modifiers->setModifiers(@opt_modifiers);
    return $ruling_object;
}
