#!/usr/bin/env perl
use warnings;
use strict;
use v5.10.0;

# on Cygwin:
#     apt-cyg install libpaper1 libpaper-bin
# on other systems:
#     it's part of something called libpaper.
#     git@github.com:naota/libpaper.git
# to list all paper sizes and their widths and heights:
#     paperconf -a -n -s
# to get the default paper size:
#     paperconf

our $opt_papersize;
our $opt_width;
our $opt_height;
our $opt_unit_type;
our $opt_color_type;
our @opt_modifiers;
our $opt_list_ruling_names;
our $opt_list_rulings;

our $opt_id;
our $opt_filename;

our $opt_2up;
our $opt_2page;
our $opt_pdf;
our $opt_ps;

our $opt_dry_run;
our $opt_verbose = 0;

our $opt_line_width;
our $opt_feint_line_width;
our $opt_dot_width;
our $opt_margin_line_width;
our $opt_dpi;

our $opt_line_color;
our $opt_feint_line_color;
our $opt_dot_color;
our $opt_margin_line_color;
our $opt_color;

use lib "$ENV{HOME}/git/dse.d/utilities/lib";
use My::Text::Table;

use Getopt::Long;
use Data::Dumper;
use Carp::Always;

use FindBin;
use lib "${FindBin::Bin}/../lib";

Getopt::Long::Configure(qw(bundling gnu_compat no_ignore_case));
Getopt::Long::GetOptions(
    "M|p|media|paper|paper-size=s"    => \$opt_papersize,
    "w|width=s"                       => \$opt_width,
    "h|height=s"                      => \$opt_height,
    "g|gray|grey|grayscale|greyscale" => sub { $opt_color_type = "grayscale"; },
    "b|black"                         => sub { $opt_color_type = "black";     },
    "c|color|colour"                  => sub { $opt_color_type = "color";     },
    "m|metric"                        => sub { $opt_unit_type = "metric";     },
    "i|imperial"                      => sub { $opt_unit_type = "imperial";   },
    "modifier|modifiers=s"            => sub { push(@opt_modifiers, split(qr{\s+|\s*,\s*}, $_[1])); },
    'id=s'                            => \$opt_id,
    'filename=s'                      => \$opt_filename,
    'list-ruling-names'               => \$opt_list_ruling_names,
    'list-rulings'                    => \$opt_list_rulings,
    'two-up|generate-two-up|generate-2up'           => \$opt_2up,
    'two-page|generate-two-page|generate-2page'     => \$opt_2page,
    'ps|postscript|generate-ps|generate-postscript' => \$opt_ps,
    'pdf|generate-pdf'                              => \$opt_pdf,
    'n|dry-run'           => \$opt_dry_run,
    'v|verbose+'          => \$opt_verbose,
    'dpi=f'               => \$opt_dpi,
    'line-width=f'        => \$opt_line_width,
    'feint-line-width=f'  => \$opt_feint_line_width,
    'dot-width=f'         => \$opt_dot_width,
    'margin-line-width=f' => \$opt_margin_line_width,
    'line-color=s'        => \$opt_line_color,
    'feint-line-color=s'  => \$opt_feint_line_color,
    'dot-color=s'         => \$opt_dot_color,
    'margin-line-color=s' => \$opt_margin_line_color,
    'color=s'             => \$opt_color,
    "help|?" => sub {
        usage();
        exit(0);
    }
) or die("Type '$0 --help' for help.\n");

sub usage { print <<"EOF"; }
usage:
    $0 [<option> ...] <ruling>
options:
    -M, -p, --media, --paper-size=<size>        8.5x11in, letter, etc.
        -w, --width=<width>                     0.5in, 36, 36pt, etc.
        -h, --height=<height>
        -g, --gray, --grayscale
        -b, --black
        -c, --color, --colour
        -m, --metric
        -i, --imperial
            --modifier=<modifier>
            --id=<id>
            --filename=<filename>    output SVG to file instead of stdout
list of <ruling>s:
    --list-ruling-names
    --list-rulings                   show some details
if --filename is specified:
    --pdf           generate PDF file(s)
    --ps            generate PS file(s)
    --two-page      also generate 2-page file(s) if --pdf and/or --ps
    --two-up        also generate 2-up file(s) if --pdf and/or --ps
ruling line widths:
    --dpi=<dpi>                    printer dots per inch (default 600)
    --line-width=<dots>
    --feint-line-width=<dots>
    --dot-width=<dots>
    --margin-line-width=<dots>
ruling line colors:
    --line-color=<color>           '#rrggbb[aa]', 'rgb()', 'rgba()' style colors
    --feint-line-color=<color>
    --dot-color=<color>
    --margin-line-color=<color>
    --color=<color>                specify line, feint line, and dot color
miscellaneous:
    -n, --dry-run
    -v, --verbose
    -?, --help
EOF

use My::Printable::Document;
use My::Printable::Ruling;
use My::Printable::Unit qw(:const);

if (defined $opt_pdf && !defined $opt_filename) {
    die("--pdf has no effect without --filename\n");
}
if (defined $opt_ps && !defined $opt_filename) {
    die("--ps has no effect without --filename\n");
}
if (defined $opt_2up && !defined $opt_filename) {
    die("--two-up has no effect without --filename\n");
}
if (defined $opt_2page && !defined $opt_filename) {
    die("--two-page has no effect without --filename\n");
}

if ($opt_list_ruling_names || $opt_list_rulings) {
    use My::Printable::Util::Installed;
    my $inst = My::Printable::Util::Installed->new();
    if ($opt_list_ruling_names) {
        say foreach @{$inst->rulingNameList};
    }
    if ($opt_list_rulings) {
        my $tt = My::Text::Table->new();
        $tt->header(
            'Ruling',
            'Color Type',
            'Lines',
            'Feint Lines',
            'Dots',
            'Margin Lines',
        );
        foreach my $ruling_name (@{$inst->rulingNameList}) {
            my $count = 0;
            foreach my $colorType ('color', 'grayscale', 'black') {
                $count += 1;
                my $object = create_ruling_object($ruling_name);
                $object->colorType($colorType);
                my $lineWidth          = eval { $object->lineWidth       / PD * PT; } // 0;
                my $feintLineWidth     = eval { $object->feintLineWidth  / PD * PT; } // 0;
                my $dotWidth           = eval { $object->dotWidth        / PD * PT; } // 0;
                my $marginLineWidth    = eval { $object->marginLineWidth / PD * PT; } // 0;
                $tt->addRow(
                    $count == 1 ? $ruling_name : '',
                    $colorType,
                    sprintf('%.3g', $lineWidth),
                    sprintf('%.3g', $feintLineWidth),
                    sprintf('%.3g', $dotWidth),
                    sprintf('%.3g', $marginLineWidth),
                );
            }
        }
        print $tt->as_string;
    }
    exit(0);
}

if (scalar @ARGV != 1) {
    die("must specify a ruling name\n");
}

foreach my $ruling_name (@ARGV) {
    my $object = create_ruling_object($ruling_name);
    $object->generate();
    $object->print();
}

sub create_ruling_object {
    my ($ruling_name, @args) = @_;
    my $ruling_class_name = My::Printable::Ruling->getRulingClassName($ruling_name);
    eval "use $ruling_class_name";
    if ($@) {
        die $@;
    }
    my $object = $ruling_class_name->new();
    $object->id($opt_id)                   if defined $opt_id;
    $object->filename($opt_filename)       if defined $opt_filename;
    $object->paperSizeName($opt_papersize) if defined $opt_papersize;
    $object->width($opt_width)             if defined $opt_width;
    $object->height($opt_height)           if defined $opt_height;
    $object->unitType($opt_unit_type)      if defined $opt_unit_type;
    $object->colorType($opt_color_type)    if defined $opt_color_type;
    $object->generate2Page($opt_2page)     if defined $opt_2page;
    $object->generate2Up($opt_2up)         if defined $opt_2up;
    $object->generatePS($opt_ps)           if defined $opt_ps;
    $object->generatePDF($opt_pdf)         if defined $opt_pdf;
    $object->dryRun($opt_dry_run)          if defined $opt_dry_run;
    $object->verbose($opt_verbose)         if defined $opt_verbose;
    $object->dpi($opt_dpi)                           if defined $opt_dpi;
    $object->lineWidth($opt_line_width)              if defined $opt_line_width;
    $object->feintLineWidth($opt_feint_line_width)   if defined $opt_feint_line_width;
    $object->dotWidth($opt_dot_width)                if defined $opt_dot_width;
    $object->marginLineWidth($opt_margin_line_width) if defined $opt_margin_line_width;
    $object->lineColor($opt_line_color // $opt_color)              if defined $opt_line_color || defined $opt_color;
    $object->feintLineColor($opt_feint_line_color // $opt_color)   if defined $opt_feint_line_color || defined $opt_color;
    $object->dotColor($opt_dot_color // $opt_color)                if defined $opt_dot_color || defined $opt_color;
    $object->marginLineColor($opt_margin_line_color)               if defined $opt_margin_line_color;
    $object->modifiers->set(@opt_modifiers);
    return $object;
}
