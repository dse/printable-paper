#!/usr/bin/env perl
use warnings;
use strict;
use FindBin;
use lib "${FindBin::Bin}/../lib";

use My::RuledPaper;
use My::RuledPaper::MonkeyPatch::Drawing;
use My::RuledPaper::Constants qw(:all);
use POSIX qw(floor);

draw_paper(feints => 5,
           major_spacing => 1 * IN,
           margin => 0.16 * IN,
           is_dot_grid => 1,
           paper_size => {LETTER});

sub draw_paper {
    my %args = @_;

    my $non_repro_blue = COLOR_NON_REPRO_BLUE;
    my %paper_size = %{$args{paper_size}};
    my $is_dot_grid = $args{is_dot_grid};

    my $major_pen_width;
    my $minor_pen_width;

    if ($is_dot_grid) {
        $major_pen_width = 4/600 * IN;
        $minor_pen_width = 10/600 * IN;
    } else {
        $major_pen_width = 10/600 * IN;
        $minor_pen_width = 2/600 * IN;
    }

    my $major_spacing = $args{major_spacing};
    my $feints = $args{feints};
    my $margin = $args{margin};

    my $p = My::RuledPaper->new();
    $p->{width} = $paper_size{width};
    $p->{height} = $paper_size{height};
    $p->{style} = <<"END";
        line, rect {
            fill: none;
            stroke-width: ${minor_pen_width}px;
            stroke: ${non_repro_blue};
            stroke-linecap: round;
            stroke-linejoin: round;
        }
        .major {
            stroke-width: ${major_pen_width}px;
        }
        .fill {
            fill: ${non_repro_blue};
        }
        circle {
            fill: ${non_repro_blue};
        }
END

    our $x_groups = floor(($p->{width} - 2 * $margin) / $major_spacing);
    our $y_groups = floor(($p->{height} - 2 * $margin) / $major_spacing);
    our $x0 = $p->{width} / 2 - ($x_groups * $major_spacing) / 2;
    our $y0 = $p->{height} / 2 - ($y_groups * $major_spacing) / 2;

    my @x1 = -1 .. ($x_groups + 1);
    my @y1 = -1 .. ($y_groups + 1);
    my @x2 = 1 - $feints .. (($x_groups + 1) * $feints - 1);
    my @y2 = 1 - $feints .. (($y_groups + 1) * $feints - 1);

    my @x_minor_only = map { $x0 + $_ * ($major_spacing / $feints) } grep { $_ % $feints != 0 } @x2;
    my @y_minor_only = map { $y0 + $_ * ($major_spacing / $feints) } grep { $_ % $feints != 0 } @y2;
    my @x_minor      = map { $x0 + $_ * ($major_spacing / $feints) } @x2;
    my @y_minor      = map { $y0 + $_ * ($major_spacing / $feints) } @y2;
    my @x_major      = map { $x0 + $_ * $major_spacing } @x1;
    my @y_major      = map { $y0 + $_ * $major_spacing } @y1;

    @x_minor_only = grep { $_ >= 0 && $_ <= $p->{width}  } @x_minor_only;
    @y_minor_only = grep { $_ >= 0 && $_ <= $p->{height} } @y_minor_only;
    @x_minor      = grep { $_ >= 0 && $_ <= $p->{width}  } @x_minor;
    @y_minor      = grep { $_ >= 0 && $_ <= $p->{height} } @y_minor;
    @x_major      = grep { $_ >= 0 && $_ <= $p->{width}  } @x_major;
    @y_major      = grep { $_ >= 0 && $_ <= $p->{height} } @y_major;

    if ($is_dot_grid) {
        # minor dot grid
        foreach my $x (@x_minor_only) {
            foreach my $y (@y_minor_only) {
                $p->circle($x, $y, $minor_pen_width / 2, class => 'fill');
            }
        }
    } else {
        # minor line grid
        foreach my $x (@x_minor_only) {
            $p->line($x, 0, $x, $p->{height}, class => 'minor');
        }
        foreach my $y (@y_minor_only) {
            $p->line(0, $y, $p->{width}, $y, class => 'minor');
        }
    }

    # major line grid
    foreach my $x (@x_major) {
        $p->line($x, 0, $x, $p->{height}, class => 'major');
    }
    foreach my $y (@y_major) {
        $p->line(0, $y, $p->{width}, $y, class => 'major');
    }

    print($p->svg());
}
